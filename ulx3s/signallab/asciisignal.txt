
\ Requires double, fixpoint, bitlog and fourier.

\ ------------------------------------------------------------------------------
\  Heapsort
\  https://rosettacode.org/wiki/Sorting_algorithms/Heapsort#Forth
\ ------------------------------------------------------------------------------

: r'@ r> r> r@ swap >r swap >r ;

' < variable heapsort-comparison

: precedes ( n1 n2 a -- f) >r cells r@ + @ swap cells r> + @ swap heapsort-comparison @ execute ;
: exchange ( n1 n2 a --  ) >r cells r@ + swap cells r> + over @ over @ swap rot ! swap ! ;

: siftDown                             ( a e s -- a e s)
  swap >r swap >r dup                  ( s r)
  begin                                ( s r)
    dup 2* 1+ dup r'@ <                ( s r c f)
  while                                ( s r c)
    dup 1+ dup r'@ <                   ( s r c c+1 f)
    if                                 ( s r c c+1)
      over over r@ precedes if swap then
    then drop                          ( s r c)
    over over r@ precedes              ( s r c f)
  while                                ( s r c)
    tuck r@ exchange                   ( s r)
  repeat then                          ( s r)
  drop drop r> swap r> swap            ( a e s)
;

: heapsort-core                        ( a n --)
  over >r                              ( a n)
  dup 1- 1- 2/                         ( a c s)
  begin                                ( a c s)
    dup 0< 0=                          ( a c s f)
  while                                ( a c s)
    siftDown 1-                        ( a c s)
  repeat drop                          ( a c)

  1- 0                                 ( a e 0)
  begin                                ( a e 0)
    over 0>                            ( a e 0 f)
  while                                ( a e 0)
    over over r@ exchange              ( a e 0)
    siftDown swap 1- swap              ( a e 0)
  repeat                               ( a e 0)
  drop drop drop r> drop
;


: heapsort-signed ( addr len -- )
  [']  < heapsort-comparison !
  heapsort-core
;

: heapsort-unsigned ( addr len -- )
  ['] u< heapsort-comparison !
  heapsort-core
;

\ ------------------------------------------------------------------------------
\  Switch between unsigned and signed, necessary for 16 Bit ADCs
\ ------------------------------------------------------------------------------

: un<->signed ( -- ) \ 0 .. 65535 <--> -32768 .. 32767
  N_WAVE 0 do
    i dup fft-real@ $8000 xor swap fft-real!
    \ i fft-real@ $8000 xor i fft-real!
  loop
;

: un<->signed-imag ( -- ) \ 0 .. 65535 <--> -32768 .. 32767
  N_WAVE 0 do
    i dup fft-imag@ $8000 xor swap fft-imag!
    \ i fft-imag@ $8000 xor i fft-imag!
  loop
;

\ ------------------------------------------------------------------------------
\  ASCII art tools
\ ------------------------------------------------------------------------------


: f.nr7 ( f -- ) ( f-Low f-High n -- ) \ Prints a s15.16 number

  >r ( Low High R: n )

  dup 0< >r
  dabs
  ( uLow uHigh )
  0 <# #s   ( uLow 0 0 )

  r> if [char] - hold then

  drop swap ( 0 uLow )

  [char] , hold<
  r> 0 ?do f# loop

  #> dup 12 swap - spaces

  type space
;

: dashes 0 ?do [char] - emit loop ;

: airbar ( min max -- )
    2dup =
    if
      drop
      spaces [char] | emit
    else
      over      spaces [char] < emit
      swap - 1- spaces [char] > emit
    then
;

: fillbar ( min max -- )
    2dup =
    if
      drop
      spaces [char] | emit
    else
      over      spaces [char] < emit
      swap - 1- dashes [char] > emit
    then
;

\ ------------------------------------------------------------------------------
\  Signal statistics
\ ------------------------------------------------------------------------------

: stats ( -- avg min max )
  0.
  N_WAVE 0 do
    i fft-real@  0 d+
  loop
  \ N_WAVE um/mod nip
  LOG2_N_WAVE 2rshift drop

  -1
  N_WAVE 0 do
    i fft-real@ umin
  loop

  0
  N_WAVE 0 do
    i fft-real@ umax
  loop
;

: stats-signed ( -- avg min max )
  0.
  N_WAVE 0 do
    i fft-real@  m+
  loop
  \ N_WAVE sm/rem nip
  LOG2_N_WAVE 2arshift drop

  32767
  N_WAVE 0 do
    i fft-real@ min
  loop

  -32768
  N_WAVE 0 do
    i fft-real@ max
  loop
;

: printstats ( -- )
  cr
  stats
  rot  ." Avg: " 6 u.r cr
  swap ." Min: " 6 u.r cr
       ." Max: " 6 u.r cr
;

: printstats-signed ( -- )
  cr
  stats-signed
  rot  ." Avg: " 6 .r cr
  swap ." Min: " 6 .r cr
       ." Max: " 6 .r cr
;


: printbin-signed ( bin count -- )
  dup if swap 6 .r 6 .r cr else 2drop then
;

: signal-binning-signed ( -- )

  cr
  13 dashes cr
  ."    ADC     #" cr
  13 dashes

  fft-real fft-imag N_WAVE cells move
  fft-imag N_WAVE heapsort-signed

  cr
  -32768 0
  N_WAVE 0 do
    over i fft-imag@ =
    if 1+
    else
      printbin-signed
      i fft-imag@ 1
    then
  loop
  printbin-signed

  fft-imag N_WAVE cells 0 fill
;

: printbin ( bin count -- )
  dup if swap 6 u.r 6 u.r cr else 2drop then
;

: signal-binning ( -- )

  cr
  13 dashes cr
  ."    ADC     #" cr
  13 dashes

  fft-real fft-imag N_WAVE cells move
  fft-imag N_WAVE heapsort-unsigned

  cr
  0 0
  N_WAVE 0 do
    over i fft-imag@ =
    if 1+
    else
      printbin
      i fft-imag@ 1
    then
  loop
  printbin

  fft-imag N_WAVE cells 0 fill
;

\ ------------------------------------------------------------------------------
\  Signal plotters
\ ------------------------------------------------------------------------------

: u*/ ( a b c -- d ) >r um* r> um/mod nip ;

80 13 - constant WIDTH

: signalplot-lin-bounds ( umin umax -- )

  cr
  13 spaces ." | " over 6 u.r ."    Lin" WIDTH 22 - spaces dup 6 u.r ."  |"
  cr
  13 spaces
  WIDTH 0 do [char] = emit loop cr

  2dup = if 1+ then \ Prevent divide by zero later!
  over - ( min range )

  N_WAVE 0 do
    i 6 u.r
    over i fft-real@ dup 6 u.r space
    swap - over WIDTH 1- swap u*/
    spaces [char] | emit cr
  loop

  13 spaces
  WIDTH 0 do [char] = emit loop cr

  2drop
;

: signalplot-lin-bounds-signed ( min max -- )

  cr
  13 spaces ." | " over 6 .r ."    Lin" WIDTH 22 - spaces dup 6 .r ."  |"
  cr
  13 spaces
  WIDTH 0 do [char] = emit loop cr

  2dup = if 1+ then \ Prevent divide by zero later!
  over - ( min range )

  N_WAVE 0 do
    i 5 u.r
    over i fft-real@ dup 7 .r space
    swap - over WIDTH 1- swap u*/
    spaces [char] | emit cr
  loop

  13 spaces
  WIDTH 0 do [char] = emit loop cr

  2drop
;

: signalplot-log-bounds ( umin umax -- )

  cr
  13 spaces ." | " over 6 u.r ."    Log" WIDTH 22 - spaces dup 6 u.r ."  |"
  cr
  13 spaces
  WIDTH 0 do [char] = emit loop cr

  bitlog swap bitlog swap

  2dup = if 1+ then \ Prevent divide by zero later!
  over - ( min range )

  N_WAVE 0 do
    i 6 u.r
    over i fft-real@ dup 6 u.r space bitlog
    swap - over WIDTH 1- swap u*/
    spaces [char] | emit cr
  loop

  13 spaces
  WIDTH 0 do [char] = emit loop cr

  2drop
;

100,0 2variable samplerate

: fft-plot ( -- ) \ FFT output is always signed
  cr
  13 spaces ." | " 0 6 u.r ."    Log" WIDTH 22 - spaces $7FFF 6 u.r ."  |"
  cr
  13 spaces
  WIDTH 0 do [char] = emit loop cr

  N_WAVE
\        0 do  \ Symmetric printout
  N_WAVE 2/ do  \ Start with DC component


    i N_WAVE 2/ - s>f N_WAVE s>f f/ samplerate 2@ f* 3 f.nr7

    i fft-real@ abs bitlog  WIDTH 1- $7FFF bitlog u*/
    i fft-imag@ abs bitlog  WIDTH 1- $7FFF bitlog u*/

    2dup umax >r
         umin r> ( min max )

    fillbar
    cr

  loop

  13 spaces
  WIDTH 0 do [char] = emit loop cr
;


: signalplot-lin ( -- )
  0 65535
  signalplot-lin-bounds
;

: signalplot-lin-min-max ( -- )
  stats rot drop
  signalplot-lin-bounds
;


: signalplot-lin-signed ( -- )
  -32768 32767
  signalplot-lin-bounds-signed
;

: signalplot-lin-min-max-signed ( -- )
  stats-signed rot drop
  signalplot-lin-bounds-signed
;


: signalplot-log ( -- )
  0 65535
  signalplot-log-bounds
;

: signalplot-log-max ( -- )
  0 stats rot drop nip
  signalplot-log-bounds
;
